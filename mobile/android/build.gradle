// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

ext.kotlin_version = '1.3.21'

/**
 * Versioning by package.json
 * Inspired by https://medium.com/@andr3wjack/versioning-react-native-apps-407469707661
 */
ext.versioning = [
    getNpmVersion: { ->
        def inputFile = new File("../package.json")
        def packageJson = new JsonSlurper().parseText(inputFile.text)
        return packageJson["version"]
    },
    getNpmVersionArray: { -> // major [0], minor [1], patch [2]
        def (major, minor, patch) = versioning.getNpmVersion().tokenize('.')
        return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(patch)] as int[]
    }
]

subprojects {
    ext {
        def npmVersion = versioning.getNpmVersionArray()
        versionMajor = npmVersion[0]
        versionMinor = npmVersion[1]
        versionPatch = npmVersion[2]
    }
}

buildscript {
    ext {
        minSdkVersion = 21
        targetSdkVersion = 28
        compileSdkVersion = 28
        supportLibVersion = "27.1.0"
        googlePlayServicesVersion = "16.0.0"
        firebaseVersion = "17.3.4"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        // Release builds crash with 3.4.0 and 3.4.1
        // https://github.com/facebook/react-native/issues/18357#issuecomment-437186504
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath 'com.google.gms:google-services:4.0.2'
        classpath 'com.google.android.gms:strict-version-matcher-plugin:1.0.2'
    }
}

allprojects {
    repositories {
        configurations.all {
            resolutionStrategy {
                force 'com.android.support:support-core-utils:27.1.0'
                force 'com.android.support:support-core-ui:27.1.0'
                force 'com.android.support:support-core-ui:27.1.0'
                force 'com.android.support:support-fragment:27.1.0'
                force 'com.android.support:support-compat:27.1.0'
                force 'com.android.support:support-annotations:27.1.0'
                force 'android.arch.lifecycle:common:1.1.0'
                force 'android.arch.core:common:1.1.0'
                // Not sure what needs this
                force "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
            }
        }

        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        flatDir {
            dirs "$rootDir/../node_modules/react-native-samsung-bks/android/aar"
        }
        maven { url "https://jitpack.io" }
        maven { url "https://maven.google.com" }
        google()
        mavenLocal()
        jcenter()
    }
}


wrapper {
    gradleVersion = '5.2'
    distributionUrl = distributionUrl.replace("bin", "all")
}
